# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=curl
pkgname=('curl' 'libcurl' 'libcurl-devel')
pkgver=7.58.0
pkgrel=9001
pkgdesc="Multi-protocol file transfer utility"
arch=('i686' 'x86_64')
url="https://curl.haxx.se"
license=('MIT')
makedepends=('zlib-devel')
options=('!libtool' 'strip' '!debug')
source=("https://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2"{,.asc}
        curl-7.55.1-msys2.patch
        curl-7.58.0-libpsl-static-libs.patch)
sha256sums=('1cb081f97807c01e3ed747b6e1c9fee7a01cb10048f1cd0b5f56cfe0209de731'
            'SKIP'
            '2e6e2245a10890f8a8b1ec84dbe86dd2415b05359a93e69be9c7b90768e6ccc0'
            '5d6563e29e85c7fadd129308e9927450c59fe5d429d9b27aafd3460bfc40defb')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'   # Daniel Stenberg
              '914C533DF9B2ADA2204F586D78E11C6B279D5C91')  # Daniel Stenberg (old key)

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/curl-7.58.0-libpsl-static-libs.patch
  patch -p1 -i ${srcdir}/curl-7.55.1-msys2.patch
  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --enable-shared \
    --enable-static \
    --disable-debug \
    --enable-optimize \
    --enable-ipv6 \
    --disable-hidden-symbols \
    --disable-ares \
    --with-gssapi \
    --enable-sspi \
    --enable-ldap \
    --enable-ldaps \
    --without-libidn2 \
    --without-libmetalink \
    --without-librtmp \
    --without-libssh2 \
    --without-spnego \
    --without-random \
    --with-ssl \
    --with-wissl \
    --without-winidn \
    --without-gnutls \
    --without-nghttp2 \
    --with-zlib \
    --with-libpsl \
    --without-ca-bundle \
    --without-ca-path \
    ac_cv_func_setmode=yes

  make
  make DESTDIR="${srcdir}/dest" install
}

package_curl() {
  depends=('libcurl' 'zlib')
  groups=('base')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/

  # license
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_libcurl() {
  pkgdesc="Multi-protocol file transfer library (runtime)"
  depends=('zlib')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_libcurl-devel() {
  pkgdesc="Libcurl headers and libraries"
  depends=("libcurl=${pkgver}" 'zlib-devel')
  options=('staticlibs')
  groups=('development')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
