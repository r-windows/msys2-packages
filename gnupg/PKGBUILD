# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gnupg
pkgver=1.4.23
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='https://gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('gettext-devel'
             #'libassuan-devel'
             'libbz2-devel'
             #'libksba-devel'
             'libbz2-devel'
             'libcurl-devel'
             #'libgcrypt-devel'
             #'libgnutls-devel'
             'libiconv-devel'
             #'libnpth-devel'
             'libreadline-devel'
             'tar'
             'zlib-devel')
optdepends=('curl: gpg2keys_curl')
depends=('bzip2'
         #'libassuan'
         'libbz2'
         'libcurl'
         #'libgcrypt'
         #'libgnutls'
         'libiconv'
         'libintl'
         #'libksba'
         #'libnpth'
         'libreadline'
         #'pinentry'
         'zlib'
        )
source=("https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        'gnupg-1.4.20-msys2.patch'
	'0001-Introduce-GPG_ASKPASS-support.patch')
sha256sums=('c9462f17e651b6507848c08c430c791287cd75491f8b5a8b50c6ed46b12678ba'
            'SKIP'
            '37dc00f02d5cb04880e8dc286e80ea2a000b7357002a44cdbebe495d43fa5980'
            '3e6cfc7916aa2057374f0b455ce59ba82d08fafc34e3ab06688acf52f927acf0')

install=${pkgname}.install

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/gnupg-1.4.20-msys2.patch
  patch -p1 -i ${srcdir}/0001-Introduce-GPG_ASKPASS-support.patch

  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --enable-maintainer-mode

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # For gnupg 2.x
  # ln -s "${pkgdir}"/usr/bin/gpg{2,}
  # #ln -s "${pkgdir}"/usr/bin/gpgv{2,}
  # ln -s "${pkgdir}"/usr/share/man/man1/gpg{2,}.1
  # rm "${pkgdir}/usr/share/gnupg/com-certs.pem" # FS#33059
}
