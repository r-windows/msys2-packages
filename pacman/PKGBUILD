# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Rtools: Jeroen Ooms <jeroenooms@gmail.com>
# Note: pacman statically links libcurl, so custom curl needs to be available at build time.
pkgname=pacman
pkgver=5.2.1
pkgrel=9500
contrib_ver=1.2.0
pkgdesc="A library-based package manager with dependency support (MSYS2 port)"
arch=('i686' 'x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash>=4.2.045'
         #'dash'
         #'busybox'
         'expat'
         'gettext'
         'gnupg'
         'msys2-runtime'
         #'libarchive'
         #'curl'
         'curl-ca-bundle'
         #'libgpgme'
         'pacman-mirrors'
         'msys2-keyring'
         'which'
         'bzip2'
         'xz')
optdepends=('diffutils' 'vim')
checkdepends=('python2')
makedepends=('asciidoc'
             'autoconf'
             'automake-wrapper'
             'doxygen'
             'libtool'
             'git'
             'gettext-devel'
             'heimdal-devel'
             'libarchive-devel'
             'libcurl-devel'
             'libexpat-devel'
             'libgpgme-devel'
             'libsqlite-devel'
             'libunistring-devel'
             'pkg-config')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
# options=('!strip' 'debug')
backup=("etc/pacman.conf"
        "etc/makepkg.conf"
        "etc/makepkg_mingw32.conf"
        "etc/makepkg_mingw64.conf")
source=(https://sources.archlinux.org/other/pacman/${pkgname}-${pkgver}.tar.gz{,.sig}
        https://git.archlinux.org/pacman-contrib.git/snapshot/${pkgname}-contrib-${contrib_ver}.tar.gz
        "pacman.conf"
        "makepkg.conf"
        "makepkg_mingw32.conf"
        "makepkg_mingw64.conf"
        "makepkg-mingw"
        "0000-pacman-msysize.patch"
        "0001-more-debugging-info.patch"
        "0002-Add-util-msys2.-c-h-and-rebase-db-msys2.-c.patch"
        "0003-use-busybox-for-msys2-post-installs.patch"
        "0004-Link-pacman-with-static-libraries.patch"
        "0005-Hack-gettext-libalpm-pkg-config-static-link.patch"
        "0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch"
        "0007-exe-interp-ignore-file-conflict.patch"
        "0008-answer-yes-by-default.patch"
        "0009-msys-use-pipe-instead-socket.patch"
        "0100-contrib.patch"
        "0101-contrib-pactree-gpgdir.patch"
        "0010-filelist-strcasecmp.patch"
        Doxyfile.in)
sha256sums=('1930c407265fd039cb3a8e6edc82f69e122aa9239d216d9d57b9d1b9315af312'
            'SKIP'
            '317f53819e35647a19138cb0d68e16206af4a80f52115a7cd622c4a367f914b7'
            'b2e043d2719240c87ce4ac90b44a8dbd135eeaf4101df6e500a606cda0e15f28'
            '3d2f8a8c363a76ebd417e883cf28dc093e611b1360fe684d40e29540cbcb7ef2'
            '7a345127473629fe79c99bb16194e201989b2ebae5de37af98b2ae2e5488f97d'
            'f2d127a03c8e47660cd2b0017453e74402470824b23139e1003b8a081e98e3bd'
            '684648d5b1246af9ce5b3afaadf201873269c5208057e8cbdbd409b4c0f22564'
            'e1f880cbfea6e1d8d67cdb17bc07eaaa99421f65e688e6858ccbcbbb9a6b69d3'
            '24ea2c8dca37847e04894ebfd05d1cf5df49dc0c8089f5581c99caa19b77a7ef'
            '870b197b7d6379a9c1ebb5c449c902b21d75ec21e966a2e54af82501465180f7'
            '23132552a388b238acf8bf650b5c2aa08cf3de63c647e84ad551807c4edfeb1e'
            '65906109a7b71a9b836566ea888532b41f4ba6ea5996f1f3303c5789e03960cb'
            '5d2b274cc8e3f68c7b0b74db466006f52610be9b35179d8cd854a9611c981de6'
            'e2731c4007a7c78848679e2203c35d1e56d3ef15a7586aeac9cc9e6e97824cfb'
            '52343bbf99da6408950e34dd84319e34724040f866c497c25e36b4ee54030e82'
            'e4f6e17af19e17e745a9f1c6b8402f5896229062c82167cb61f8e7d29eda716c'
            '9e8fe5ee78192b0407e80ad2e52cb27569c35974b6c26e465e3d55e19c03d108'
            '78a314a11377f7c9eb7bcd5f103a32734f2df0b3696df50bd38c7f6c754154d0'
            '4ff3f10a99e894f3ae8576227f60bff7e21a1d7a775f89d936e43501693e9618'
            '93be4523fb8c3dd6b56eddfe0b09e666725a62eb43392fee336ba1a328f9ffdd'
            '8cc0a7b1a3390a8990cef242ac4a84d4eb03f367bc2a01c883908ff7c8d59427')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/0000-pacman-msysize.patch
  patch -p1 -i ${srcdir}/0001-more-debugging-info.patch
  # patch -p1 -i "${srcdir}"/0002-Add-util-msys2.-c-h-and-rebase-db-msys2.-c.patch
  # patch -p1 -i "${srcdir}"/0003-use-busybox-for-msys2-post-installs.patch
  patch -p1 -i ${srcdir}/0004-Link-pacman-with-static-libraries.patch
  patch -p1 -i ${srcdir}/0005-Hack-gettext-libalpm-pkg-config-static-link.patch
  patch -p1 -i ${srcdir}/0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch
  patch -p1 -i ${srcdir}/0007-exe-interp-ignore-file-conflict.patch
  patch -p1 -i ${srcdir}/0008-answer-yes-by-default.patch
  patch -p1 -i ${srcdir}/0009-msys-use-pipe-instead-socket.patch
  patch -p1 -i ${srcdir}/0010-filelist-strcasecmp.patch
  
  cp ${srcdir}/Doxyfile.in doc/

  autoreconf -fi

  cd ${srcdir}/${pkgname}-contrib-${contrib_ver}
  patch -p1 -i ${srcdir}/0100-contrib.patch
  patch -p1 -i ${srcdir}/0101-contrib-pactree-gpgdir.patch
  ./autogen.sh
}

build() {

  # Temp workarounnd for libarchive bug
  export LIBS="-llz4"

  cd ${srcdir}/${pkgname}-${pkgver}

  # J: added --disable-doc because asciidoc segfaults
  PKG_CONFIG="/usr/bin/pkg-config --static" \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-static \
    --disable-shared \
    --enable-doc \
    --enable-doxygen \
    --disable-git-version \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-pkg-ext=.pkg.tar.xz

  # doxygen segfaults in the first run :( so build again
  make -j3 || make

  cd ${srcdir}/${pkgname}-contrib-${contrib_ver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-git-version

  make -j1
  
}

check() {
  make -C "${pkgname}-${pkgver}" check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make -j1 DESTDIR=${pkgdir} install

  cd ${srcdir}/${pkgname}-contrib-${contrib_ver}
  make -j1 DESTDIR=${pkgdir} install

  # install Arch specific stuff
  install -dm755 ${pkgdir}/etc
  install -m644 ${srcdir}/pacman.conf ${pkgdir}/etc/pacman.conf

  case "${CARCH}" in
  i686)
    mycarch="i686"
    mychost="i686-pc-msys"
    myflags="-march=i686"
  ;;
  x86_64)
    mycarch="x86_64"
    mychost="x86_64-pc-msys"
    myflags="-march=x86-64"
  ;;
  esac

  install -m644 ${srcdir}/makepkg.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_mingw32.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_mingw64.conf ${pkgdir}/etc/
  install -m755 ${srcdir}/makepkg-mingw ${pkgdir}/usr/bin/

  # set things correctly in the default conf file
  sed -i ${pkgdir}/etc/makepkg.conf \
    -e "s|@CARCH[@]|${mycarch}|g" \
    -e "s|@CHOST[@]|${mychost}|g" \
    -e "s|@CARCHFLAGS[@]|${myflags}|g"
}
